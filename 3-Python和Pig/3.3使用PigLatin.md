<h2>使用pig Latin</h2>

本节描述了pig latin的基本概念, 帮助那些新的用户理解和编写基本的pig脚本。有关语言的更全面的概述,请访问在线文档[Pig online documentation](http://pig.apache.org/docs/r0.14.0/index.html)。

本节中加载和处理的数据，都是以tab键分割的文本内容的文件，如下所示:
```
john 21 3.89
sally 19 2.56
alice 22 3.76
doug 19 1.98
susan 26 3.25
```
<h3>pig Latin语法,语句</h3>
pig语句是用于处理pig数据的基本构造。每个语句都是一个运算符, 它将关系作为输入, 对该关系执行转换, 并将关系生成为输出。语句可以跨越多行, 但所有语句都必须以分号结尾(;)

每个pig脚本一般形式如下:
* 一个LOAD语句用于从文件系统中读取数据；
* 一个或多个语句用于传输数据;
* 一个dump或一个STORE语句用于查看或存储数据

<h4>1 loading data</h4>
load操作用于将数据从系统加载到pig中,load操作运算符的格式如下所示:
```
LOAD 'data' [USING function] [AS schema];
```

'data'参数用于指定数据的文件或目录名字，名字用单引号标识出。如果未指定目录名，则目录下的所有文件都会被载入。

USING关键字是可选的,用于指定用于分析传入数据的函数。如果省略USING关键字, 则使用默认加载函数PigStorage。默认分隔符是制表符 ('\t')。
as关键字允许为正在加载的数据定义架构,架构允许为各个字段声明名称和数据类型。下面的示例定义从文件input.txt加载的数据的架构。如果未定义架构,则不命名字段, 默认为bytearray类型。
```
A = LOAD 'students' AS (name:chararray, age:int);
DUMP A;
(john,21,3.89)
(sally,19,2.56)
(alice,22,3.76)
(doug,19,1.98)
(susan,26,3.25)
```

<h4>2 传输数据</h4>
pig包含许多操作运行进行复杂的转换数据。最常见的运算符 FILTE、FOREACH 和GROUP

<h5>FILTER</h5>
FILTER操作适用于元组或数据行，它选择基于条件的关系中的元组。以下事例中使用包含学生数据的关系A:

```
A = LOAD 'students' AS (name:chararray, age:int, gpa:float);
DUMP A;
(john,21,3.89)
(sally,19,2.56)
(alice,22,3.76)
(doug,19,1.98)
(susan,26,3.25)
```
以下事例将年龄低于20岁的学生过滤掉，并你数据存储到R关系中。
```
R = FILTER A BY age >= 20;
DUMP R;
(john,21,3.89)
(alice,22,3.76)
(susan,26,3.25)
```
条件语句可以使用AND、OR, NOT 运算符来创建更复杂的筛选语句。下面的示例筛选出年龄小于20或 GPA 小于或等于3.5 的任何学生, 并将结果存储在关系 R 中。
```
R = FILTER A BY (age >= 20) AND (gpa > 3.5)
DUMP R;
(john,21,3.89)
(alice,22,3.76)
```
<h5>foreach迭代</h5>
当FILTER对数据行起作用。FOREACH对数据列进行起作用, 与SQL中的SELECT语句类似。下面的示例使用星号(*)将关系A上的所有字段映射到关系X上。
```
R = FOREACH A GENERATE *;
DUMP R;
(john,21,3.89)
(sally,19,2.56)
(alice,22,3.76)
(doug,19,1.98)
(susan,26,3.25)
```
下面的示例使用字段名称来预测年龄和gpa从关系A映射到关系X的列
```
R = FOREACH A GENERATE age, gpa;
DUMP R;
(21,3.89)
(19,2.56)
(22,3.76)
(19,1.98)
(26,3.25)
```
<h5>GROUP</h5>
GROUP操作将具有相同键的元组组合到一个或多个关系中。下面的示例按年龄对学生数据进行分组, 并将结果存储到关系B中。
```
B = GROUP A BY age;
DUMP B;
(19,{(doug,19,1.98),(sally,19,2.56)})
(21,{(john,21,3.89)})
(22,{(alice,22,3.76)})
(26,{(susan,26,3.25)})
```
GROUP操作的结果是每个组具有一个元组的关系。此元组有两个字段:第一个字段是命名组,并且是分组键的类型。第二个字段是一个包,它采用原始关系的名称。为了阐明关系B的结构, 可以使用DESCRIBE和ILLUSTRATE操作。

```
DESCRIBE B;
B: {group: int,A: {(name: chararray,age: int,gpa: float)}}
ILLUSTRATE B;
------------------------------------------------------------
| B | group:int | A:bag{:tuple(name:chararray, |
age:int,gpa:float)} |
-------------------------------------------------------------
| | 19 | {(sally, 19, 2.56), (doug, 19, 1.98)} |
-------------------------------------------------------------
```
使用 FOREACH 运算符,前面提到的关系B中的字段可以通过名字被A引用。
```
C = FOREACH B GENERATE group, A.name;
DUMP C;
(19,{(doug),(sally)})
(21,{(john)})
(22,{(alice)})
(26,{(susan)})
``` 

<h5>Storing Data存储数据</h5>
STORE操作用于执行以前的pig声明和将结果存储在文件系统上。STORE操作的格式如下:
```
STORE alias INTO 'directory' [USING function];
```
其中alias是要存储的关系的名称,而"direcotry"存储目录的名称,以引号括起来。如果目录已存在,则存储操作将失败。输出文件将被命名part-nnnnn,并写入指定的目录中。USING关键字是可选的,用于指定存储数据的函数。如果省略使用关键字,则使用默认存储函数PigStorage。下面的示例指定用于存储带有管道分隔字段的文件的PigStorage函数。

```
A = LOAD 'students' AS (name:chararray, age:int, gpa:float);
DUMP A;
(john,21,3.89)
(sally,19,2.56)
(alice,22,3.76)
(doug,19,1.98)
(susan,26,3.25)
STORE A INTO 'output' USING PigStorage('|');
CAT output;
john|21|3.89
sally|19|2.56
alice|22|3.76
doug|19|1.98
susan|26|3.25
```

所提供的Pig Latin语句是很大的通用计算构造,但不能表达复杂的算法。下一节介绍如何使用 Python 扩展pig的功能

