pig是由两个主要部分组成的: 一种高级的数据流语言,称为"pg latin",以及一个分析、优化和执行pig latin脚本的引擎,并将pig脚本转换为在Hadoop集群上运行的MapReduce作业。与Java MapReduce相比, pig更容易编写、理解和维护, 因为它是一种数据转换语言, 它允许处理数据被描述为一系列转换。pig也是高度可扩展的,通过使用用户定义的函数 (UDF),允许自定义处理写入多种语言, 如Python。

pig应用的一个例子是提取, 变换, 装载(ETL)过程,描述应用程序如何从数据源中提取数据、转换数据以进行查询和分析, 并将结果加载到目标数据存储区。一旦pig完加载数据, 它就可以执行投影、迭代和其他转换。UDF允许在转换阶段应用更复杂的算法。在处理完数据后, 它可以存储在HDFS。

本章以pig脚本为例开始。然后介绍pig和pig latin并且以例子详细地描述。本章最后解释了如何通过使用Python来扩展pig的核心功能。

<h2>使用Pig构建WordCount程序</h2>

Example3-1用pig实现WordCount程序,这个实例假设数据存储在input.txt文件中，并存放在HDFS的/user/hduser/input文件夹下，数据路径在/user/hduser/output

```
%default INPUT '/user/hduser/input/input.txt';
%default OUTPUT '/user/hduser/output';
-- Load the data from the file system into the relation records
records = LOAD '$INPUT';
-- Split each line of text and eliminate nesting
terms = FOREACH records GENERATE FLATTEN(TOKENIZE((chararray) $0)) AS word;
-- Group similar terms
grouped_terms = GROUP terms BY word;
-- Count the number of tuples in each group
word_counts = FOREACH grouped_terms GENERATE COUNT(terms), group;
-- Store the result
STORE word_counts INTO '$OUTPUT';
```

要执行pig脚本,只需从命令行调用pig并传递要运行的脚本的名称:
```
$ pig wordcount.pig
```
作业正在运行时, 会将大量文本打印到控制台。完成作业后, 将显示一条与下面类似的成功消息:
```
2015-09-26 14:15:10,030 [main] INFO org.apache.pig.backend.hadoop.executionengine.mapReduceLayer.MapReduceLauncher - Success!
2015-09-26 14:15:10,049 [main] INFO org.apache.pig.Main - Pig
script completed in 18 seconds and 514 milliseconds (18514 ms)
```
执行后输出到 HDFS 上的 /user/hduser/output/pig_word‐count/part-r-00000,其中的内容:

```
2 be
1 the
3 jack
1 over
1 quick
1 jumped
1 nimble
1 candlestick
```

<h3>执行细节</h3>
本小结，将对wordcount.pig脚本中的每个申明语句进行解释说明:

第一个语句从文件系统加载数据并将其存储在关系记录中:
```
records = LOAD '/user/hduser/input/input.txt';
```

第二个语句使用标记化函数拆分每行文本, 并使用拼合运算符消除嵌套

```
terms = FOREACH records GENERATE FLATTEN(TOKENIZE((chararray)$0)) AS word;
```

第三个语句使用组运算符对具有相同字段的元组进行分组

```
grouped_terms = GROUP terms BY word;
```

第四条语句遍历每个包中的所有数据, 并使用 COUNT 函数返回总和

```
word_counts = FOREACH grouped_terms GENERATE COUNT(terms),group;
```

第五和最后一个语句将结果存储在 HDFS 中
```
STORE word_counts INTO '/user/hduser/output/pig_wordcount'
```

